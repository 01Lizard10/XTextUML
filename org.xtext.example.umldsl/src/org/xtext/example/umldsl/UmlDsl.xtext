grammar org.xtext.example.umldsl.UmlDsl with org.eclipse.xtext.common.Terminals

generate umlDsl "http://www.xtext.org/example/umldsl/UmlDsl"

import "http://www.eclipse.org/uml2/5.0.0/UML" as uml
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
 
 
Model:
    elements+=Element*
;
 
Element:
    Class | Property | Association | Rule | Formular
;
 
Class:
	"class" name=ID "mapsTo" ref=[uml::Class|FQN]
; 
FQN returns ecore::EString:
    ID ("." ID)*
;
// Neu:
Property:
	"property" name=ID "mapsTo" ref=[uml::Property|FQN]
;

Association:
	"association" name=ID "mapsTo" ref=[uml::Association|FQN]
;

// Constraint:-----------------------------------------------------------
Rule:
	"rule" name=ID '{'
	elements += AbstractElement*
	'}'
;

AbstractElement:
	Variable | Expression 
;

Variable:
	name=ID '=' expression=Expression
;
Expression:
	PlusOrMinus
;
PlusOrMinus returns Expression:
	MulOrDiv (
		({Plus.left=current} '+' | {Minus.left=current} '-')
		right=MulOrDiv
	)*
;
MulOrDiv returns Expression:
	Atomic(
		({MulOrDiv.left=current} op=('*'|'/'))
		right=Atomic
	)*
;
Atomic returns Expression:
	'(' Expression ')'|
	{IntConstant} value=INT |
	{StringConstant} value=STRING |
	{BoolConstant} value=('true'|'false')|
	{VariableRef} variable=[Variable]
	;

// Forumlar-Creator: ----------------------------------------------------

Formular:
	"form"	name=ID '{'
	formelement += Formelement*
	'}'
;

Formelement:
	TextArea | Label | LineBreak
;

TextArea:
	"textarea" value=[Property]
;

Label:
	"label" value=[Property]
;

LineBreak:
	"lb"
;
